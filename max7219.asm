; MAX7219 driver
; SPI 8-digit LED Display Driver
;
; pins: VCC, GND, DATA, CS, CLK
;
; 16-bit instructions, 8-bit address + 8-bit data
; MSB first, latch bits on rising edge
; 16 registers
; BCD decode (0-9) is controlled per digit
;
; attached to PIA PORTB, SDAT=0,SLAT=1,SCLK=2
;

; SMITHBUG
;PUTC    EQU     $E205
;MONITR  EQU     $E01A

; for MIKBUG
PUTC	EQU	$E075
MONITR	EQU	$E0E3

STACK	EQU	$7FFF

; port addresses
PORT	EQU	$D402

; pin assignments
SDAT	EQU	0
SLAT	EQU	1
SCLK	EQU	2

CMD_NOOP  	EQU	0
CMD_DIGIT0	EQU	1
CMD_DIGIT1	EQU	2
CMD_DIGIT2	EQU	3
CMD_DIGIT3	EQU	4
CMD_DIGIT4	EQU	5
CMD_DIGIT5	EQU	6
CMD_DIGIT6	EQU	7
CMD_DIGIT7	EQU	8
CMD_DECODEMODE  EQU	9
CMD_INTENSITY   EQU	10
CMD_SCANLIMIT   EQU	11
CMD_SHUTDOWN	EQU	12
CMD_DISPLAYTEST	EQU	15

	ORG	$0100
RESET	LDS	#STACK
	JMP	MAIN

; must be within 256-byte page
;  A
; F B
;  G
; E C
;  D
;
; DP,ABCDEFG
TBL	DB	%01111110,%00110000,%01101101,%01111001,%00110011,%01011011,%01011111,%01110000
	DB	%01111111,%01111011,%01110111,%00011111,%01001110,%00111101,%01001111,%01000111

MAIN	LDX	#starts
	JSR	PUTS

	JSR	INIT

	; reset
	LDAA	#CMD_DISPLAYTEST
	CLRA
	JSR	WRITECMD

	; enable all LEDs/lines
	LDAA	#CMD_SCANLIMIT
	LDAB	#7
	JSR	WRITECMD

	; disable BCD decode on all digits
	LDAA	#CMD_DECODEMODE
	CLRB
	JSR	WRITECMD

	; clear display
	JSR	CLEAR

	; low intensity
	LDAA	#CMD_INTENSITY
	CLRB
	JSR	WRITECMD

	; turn on
	LDAA	#CMD_SHUTDOWN
	LDAB	#1
	JSR	WRITECMD

	CLRA
1:	DECA
	JSR	PUTHEX
	JSR	DELAY
	CMPA	#0
	BNE	1b

	LDX	#ends
	JSR	PUTS

	JMP	MONITR

starts	.asciz	"max7219 LED driver.\r\n"
ends	.asciz	"done.\r\n"

DELAY	LDX	#$2FFF
1:	DEX
	BNE	1b
	RTS

; char in A
PUTHEX	PSHA
	PSHB

	PSHA
	ASRA
	ASRA
	ASRA
	ASRA
	ANDA	#$0F
	TAB
	LDAA	#CMD_DIGIT1
	JSR	SETDIGIT
	PULB
	ANDB	#$0F
	LDAA	#CMD_DIGIT0
	JSR	SETDIGIT
	
	PULB
	PULA
	RTS

INIT	PSHA
	PSHB
	CLR	PORT+1                  ; select DDR
	LDAB	#(1<<SDAT)|(1<<SLAT)|(1<<SCLK)
	STAB	PORT
	LDAA	#$04			; select DATA
	STAA	PORT+1
	CLR	PORT			; set to low
	PULB
	PULA
	RTS

CLEAR	PSHA
	PSHB
	LDAA	#8
	CLRB
1:	JSR	WRITECMD
	DECA
	BPL	1b
	PULB
	PULA
	RTS

XHI	DS	1
XLO	DS	1

; A=digit
; B=value
SETDIGIT:
	PSHB
	ADDB	#<TBL
	STAB	XLO
	CLRB
	ADCB	#>TBL
	STAB	XHI
	LDX	XHI
	LDAB	,X
	JSR	WRITECMD
	PULB
	RTS

; A=command
; B=data
WRITECMD:	; sent MSB first
	PSHA
	JSR	WRITEBYTE
	TBA
	JSR	WRITEBYTE
	LDAA	#(1<<SLAT)	; pull LATCH high
	STAA	PORT
	CLRA			; pull LATCH low
	STAA	PORT
	PULA
	RTS

; A=byte
WRITEBYTE:
	PSHA
	PSHB
	LDX	#8
1:	ASLA
	ROLB
	ANDB	#(1<<SDAT)	; mask SDAT
	STAB	PORT
	ORAB	#(1<<SCLK)	; SCLK high
	STAB	PORT
	ANDB	#(1<<SDAT)	; SCLK low
	STAB	PORT
	DEX
	BNE	1b
	PULB
	PULA
	RTS

1:	JSR	PUTC
	INX
PUTS	LDAA	0,X
	BNE	1b
	RTS

	END
